import org.springframework.boot.gradle.tasks.bundling.BootJar

plugins {
    id "org.springframework.boot" version "3.4.5" apply false
    id "io.spring.dependency-management" version "1.1.4" apply false
}


group = "com.icc.qasker"
version = "1.6.5"

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.4.5"
        }
    }

    sourceCompatibility = '21'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        // ────────────────────────────────
        // jUnit 테스트
        // ────────────────────────────────
        testImplementation "org.springframework.boot:spring-boot-starter-test"
        testRuntimeOnly "org.junit.platform:junit-platform-launcher"

        // ────────────────────────────────
        // APM
        // ────────────────────────────────
        implementation "com.newrelic.agent.java:newrelic-api:8.11.0"

        // ────────────────────────────────
        // 스프링부트 web과 JPA
        // ────────────────────────────────
        implementation "org.springframework.boot:spring-boot-starter-web"
        implementation "org.springframework.boot:spring-boot-starter-data-jpa"
        runtimeOnly "com.mysql:mysql-connector-j"

        // ────────────────────────────────
        // API 문서 (Swagger)
        // ────────────────────────────────
        implementation "org.springdoc:springdoc-openapi:2.3.0"
        implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.8"

        // ────────────────────────────────
        // Lombok
        // ────────────────────────────────
        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.projectlombok:lombok"
    }

    test { useJUnitPlatform() }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += '-parameters'
    }

    tasks.withType(Jar).configureEach {
        duplicatesStrategy = DuplicatesStrategy.FAIL
    }

    tasks.withType(BootJar).configureEach {
        duplicatesStrategy = DuplicatesStrategy.FAIL
    }

}