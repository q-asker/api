import org.springframework.boot.gradle.tasks.bundling.BootJar

plugins {
    id "org.springframework.boot" version "3.4.5" apply false
    id "io.spring.dependency-management" version "1.1.4" apply false
}

group = "com.icc.qasker"
version = "1.7.0"

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:3.4.13"
        }
    }

    sourceCompatibility = '21'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        // ────────────────────────────────
        // 전 모듈 공통
        // Lombok: 보일러플레이트 코드 생성
        // Validation: DTO 검증 (Bean Validation)
        // ────────────────────────────────
        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.projectlombok:lombok"
        implementation 'org.springframework.boot:spring-boot-starter-validation'
    }

    test { useJUnitPlatform() }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += '-parameters'
    }

    tasks.withType(Jar).configureEach {
        duplicatesStrategy = DuplicatesStrategy.FAIL
    }

    tasks.withType(BootJar).configureEach {
        duplicatesStrategy = DuplicatesStrategy.FAIL
    }

}