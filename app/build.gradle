plugins {
    id 'org.springframework.boot'
    id "com.google.cloud.tools.jib" version "3.4.0"
    id "de.undercouch.download" version "5.3.0"
}

version = rootProject.version
group = rootProject.group


dependencies {

    implementation project(':auth:auth-impl')
    implementation project(':aws:aws-impl')
    implementation project(':quiz:quiz-impl')
    implementation project(':util:util-impl')
    implementation project(':global')

    implementation "org.springframework.boot:spring-boot-starter-actuator"
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

springBoot {
    buildInfo()
}

jib {
    from {
        image = "amazoncorretto:21"
    }
    to {
        image = "${project.property("DOCKER_ID")}/${project.property("DOCKER_IMAGE_NAME")}"
        tags = ["latest", rootProject.version.toString()]
        auth {
            username = project.property("DOCKER_ID")
            password = project.property("DOCKER_PASSWORD")
        }
    }
    extraDirectories {
        paths {
            path {
                setFrom(file("newrelic").toPath())
                into = "/app/newrelic"
            }
        }
    }
    container {
        def jvmHeapSize = project.property("JVM_HEAP_SIZE")

        jvmFlags = ["-Xms${jvmHeapSize}", "-Xmx${jvmHeapSize}"]

        def newRelicConfig = project.file("newrelic/newrelic.yml")
        def newRelicJar = project.file("newrelic/newrelic.jar")
        if (newRelicConfig.exists() && newRelicJar.exists()) {
            jvmFlags = jvmFlags + [
                    "-Dnewrelic.config.file=/app/newrelic/newrelic.yml",
                    "-javaagent:/app/newrelic/newrelic.jar",
            ]
        }
    }
}