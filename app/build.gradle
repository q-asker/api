plugins {
    id 'org.springframework.boot'
    id "com.google.cloud.tools.jib" version "3.4.0"
    id "de.undercouch.download" version "5.3.0"
}

dependencies {

    implementation project(':auth:auth-impl')
    implementation project(':aws:aws-impl')
    implementation project(':quiz:quiz-impl')

    implementation "org.springframework.boot:spring-boot-starter-actuator"
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

tasks.named("jib") {
    dependsOn "generateJmxConfigFiles"
}

jib {
    from {
        image = "qasker/eclipse-temurin-21-libreoffice"
    }
    to {
        image = "${project.property("DOCKER_ID")}/${project.property("DOCKER_IMAGE_NAME")}"
        tags = ["latest", rootProject.version.toString()]
        auth {
            username = project.property("DOCKER_ID")
            password = project.property("DOCKER_PASSWORD")
        }
    }
    extraDirectories {
        paths {
            path {
                setFrom(file("newrelic").toPath())
                into = "/app/newrelic"
            }
            path {
                setFrom(layout.buildDirectory.dir("jmx"))
                into = "/app/jmx"
                permissions = [
                        '/app/jmx/jmxremote.password': '600',
                        '/app/jmx/jmxremote.access'  : '600'
                ]
            }
        }
    }
    container {
        def jvmHeapSize = project.property("JVM_HEAP_SIZE")

        jvmFlags = ["-Xms${jvmHeapSize}", "-Xmx${jvmHeapSize}"]

        def newRelicConfig = project.file("newrelic/newrelic.yml")
        def newRelicJar = project.file("newrelic/newrelic.jar")
        if (newRelicConfig.exists() && newRelicJar.exists()) {
            jvmFlags = jvmFlags + [
                    "-Dnewrelic.config.file=/app/newrelic/newrelic.yml",
                    "-javaagent:/app/newrelic/newrelic.jar",
                    "-Dcom.sun.management.jmxremote",
                    "-Dcom.sun.management.jmxremote.port=9999",
                    "-Dcom.sun.management.jmxremote.rmi.port=9999",
                    "-Dcom.sun.management.jmxremote.authenticate=true",
                    "-Dcom.sun.management.jmxremote.ssl=false",
                    "-Djava.rmi.server.hostname=dev-api.q-asker.com",
                    "-Dcom.sun.management.jmxremote.access.file=/app/jmx/jmxremote.access",
                    "-Dcom.sun.management.jmxremote.password.file=/app/jmx/jmxremote.password"
            ]
        }
    }
}

tasks.register("generateJmxConfigFiles") {
    group = "build"

    def outputDirProvider = layout.buildDirectory.dir("jmx")
    outputs.dir(outputDirProvider)

    def id = project.property("JMX_ID")
    def password = project.property("JMX_PASSWORD")

    doLast {
        def actualOutputDir = outputDirProvider.get().asFile
        actualOutputDir.mkdirs()

        new File(actualOutputDir, "jmxremote.access").text =
                "$id  readwrite"

        new File(actualOutputDir, "jmxremote.password").text =
                "$id  $password"

    }
}